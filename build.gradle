//
//  Author:  github.com/luncliff (luncliff@gmail.com)
//
//  To Do:
//  - https://developer.android.com/studio/build/maven-publish-plugin?hl=en
//
import java.nio.file.Paths

println "${project.buildDir}"

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        // https://developer.android.com/studio/releases/gradle-plugin#4-0-0
        classpath 'com.android.tools.build:gradle:4.1.0' // requires Gradle 6.5+
        classpath 'de.mannodermaus.gradle.plugins:android-junit5:1.6.0+'
    }
}
repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.library'
android {
    compileSdkVersion 28
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    ndkVersion '21.3.6528147'

    // https://developer.android.com/studio/build/configure-apk-splits#configure-abi-split
    splits {
        abi {
            enable true
            universalApk true 
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86_64'
        }
    }
    defaultConfig {
        minSdkVersion       28
        targetSdkVersion    28
        versionName         '1.1.1'
        versionCode         6
        externalNativeBuild {
            cmake {
                cppFlags    '-Wall', '-Wextra', '-Wno-unused-parameter', '-fno-rtti'
                arguments   '-DANDROID_STL=c++_shared'
                targets     'muffin'
            }
        }
        // https://github.com/mannodermaus/android-junit5
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArgument 'runnerBuilder', 'de.mannodermaus.junit5.AndroidJUnit5Builder'
    }
    externalNativeBuild {
        cmake {
            version '3.13.0+'
            path    'CMakeLists.txt'
        }
    }
    sourceSets {
        main.setRoot('android')
        main {
            manifest.srcFile 'android/AndroidManifest.xml'
        }
        androidTest.setRoot('android/test')
        androidTest {
            java.srcDir 'android/test'
            assets.srcDir 'android/test/assets'
        }
        // https://developer.android.com/studio/releases/gradle-plugin#cmake-imported-targets
        main.jniLibs.srcDirs = ['jniLibs']
        androidTest.jniLibs.srcDirs = ['android/jniLibs']
    }
    buildFeatures {
        buildConfig = true
        prefab true
        prefabPublishing true
    }
    prefab {
        muffin {
            headers "include"
        }
    }
    packagingOptions {
        // pickFirst 'jniLibs/**/*.so'
        exclude 'META-INF/LICENSE*' // JUnit 5 will bundle in files
    }
    // https://developer.android.com/training/testing/unit-testing/instrumented-unit-tests
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed'
                showStandardStreams = true
                showCauses = true
                showExceptions = true
                showStackTraces = true
                exceptionFormat = 'full'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

    androidTestImplementation 'androidx.test:rules:1.2.0+'
    androidTestImplementation 'androidx.test:runner:1.2.0+'
    androidTestImplementation 'de.mannodermaus.junit5:android-test-core:1.2.0+'
    androidTestRuntimeOnly 'de.mannodermaus.junit5:android-test-runner:1.2.0+'

    androidTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.0+'
    androidTestImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.0+'
    androidTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.0+'
}
