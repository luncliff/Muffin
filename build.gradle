//
//  Author:  github.com/luncliff (luncliff@gmail.com)
//
buildscript {    
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5+'
        classpath 'de.mannodermaus.gradle.plugins:android-junit5:1.6+'
    }
}
repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.library'
android {
    compileSdkVersion 28
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    splits {
        abi {
            enable true
            universalApk true 
            reset()
            include 'armeabi-v7a',  'arm64-v8a', 'x86_64'
        }
    }
    defaultConfig {
        versionCode         5
        versionName         '1.1.0'
        minSdkVersion       28
        targetSdkVersion    28

        externalNativeBuild {
            cmake {
                cppFlags    '-Wall -Wextra'
                arguments   '-DANDROID_STL=c++_shared'
            }
        }
        // https://github.com/mannodermaus/android-junit5
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArgument 'runnerBuilder', 'de.mannodermaus.junit5.AndroidJUnit5Builder'
    }

    externalNativeBuild {
        cmake {
            version '3.10.2+'
            path    'CMakeLists.txt'
        }
    }

    sourceSets {
        main.setRoot('android')
        main {
            manifest.srcFile 'android/AndroidManifest.xml'
        }
        androidTest.setRoot('android/test')
        androidTest {
            java.srcDir 'android/test'
            assets.srcDir 'android/test/assets'
        }
        main.jniLibs.srcDirs = ['android/jniLibs']
        androidTest.jniLibs.srcDirs = ['android/jniLibs']
    }

    packagingOptions {
        // pickFirst 'android/jniLibs/**/*.so'
        exclude 'META-INF/LICENSE*' // JUnit 5 will bundle in files
    }
    // https://developer.android.com/training/testing/unit-testing/instrumented-unit-tests
    testOptions {
        unitTests.returnDefaultValues = true

        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed'
                showStandardStreams = true
                showCauses = true
                showExceptions = true
                showStackTraces = true
                exceptionFormat = 'full'
            }
        }
    } 
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    androidTestImplementation 'androidx.test:rules:1.2+'
    androidTestImplementation 'androidx.test:runner:1.2+'
    androidTestImplementation 'de.mannodermaus.junit5:android-test-core:1.2+'
    androidTestRuntimeOnly 'de.mannodermaus.junit5:android-test-runner:1.2+'

    androidTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.3+'
    androidTestImplementation 'org.junit.jupiter:junit-jupiter-params:5.3+'
    androidTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3+'
}
